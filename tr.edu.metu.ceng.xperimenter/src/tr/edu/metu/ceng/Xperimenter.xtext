grammar tr.edu.metu.ceng.Xperimenter with org.eclipse.xtext.common.Terminals

generate xperimenter "http://www.ceng.metu.edu.tr/~e1564178/xperimenter"

Model :
  (elements += Type)*
;
  
Type:
  Experiment | Design | StatAnalysis | Simulation | Variable
;
  
Experiment:
	'experiment' name = ID 
	'{'
		('desc' description = STRING ';')?
		'objective' objective = Objective ';'
		'design' design = [Design] ';'
		'simulation' simulation = [Simulation] ';'
  		'analysis' analysis = [StatAnalysis] ';'
  		'visual' visualization = VisualizationType ';'
  		('timeout' timeout = INT ';')? 
	'}'
;
 
Design:
	'design' name = ID '{'
		'method' method = SamplingMethod ';'
		'varlist' (variables += [Variable]*) ';'
	'}'
;

Simulation:
	'simulation' name = ID '{'
		'modelFile' modelFilePath = STRING ';'
		('modelType' modelType = SimModelType ';')
		(ports += Port)*	
	'}'
;

StatAnalysis:
	'statAnalysis' name=ID '{'
		'service' uri=STRING ';'
	'}'
;

Port:
	InPort | OutPort
;

InPort:
	'inport' name = ID ':' variable = [Variable] ';'
;

OutPort:
	'outport' name = ID ':' variable = [Variable] ';'
;
 
Variable:
  'variable' name = ID ':' type = VariableType 'group' group = VariableGroup ('[' lowValue = INT ',' highValue = INT ']')? ('gen' generator=[NumberGenerator])? ';'
;

NumberGenerator:
	'generator' name=ID '{'
		'service' uri=STRING ';'
		('seed' seed=INT ';')?
	'}'
;

enum Objective :
	COMPARATIVE | SCREENING | RESPSURFACE
;

enum SimModelType :
	STATIC | CONTINUOUS | DISCRETEEVENT
;

enum VariableGroup :
	FACTOR | RESPONSE 
;

enum VariableType :
	BOOLEAN | INTEGER | FLOAT | STRING  
;

enum SamplingMethod:
	RANDOMIZED | RANDOMIZEDBLOCK | FACTORIAL | FRACFACTORIAL | CENTRALCOMP
;

enum StatAnalysisType:
	HYPOTESTING | ANOVA | MANOVA | CONFINTERVAL
;

enum VisualizationType:
	HISTOGRAM | SCATTERPLOT | BARCHART
;